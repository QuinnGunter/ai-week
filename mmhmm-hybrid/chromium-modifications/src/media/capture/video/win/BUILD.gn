# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/ui.gni")
import("//media/media_options.gni")
import("//testing/test.gni")

config("seglib_import") {
  include_dirs = [ "//third_party/seglib/include/segmentation" ]
  lib_dirs = [ "//third_party/seglib/lib/" ]
  libs = [ "tpxai_segmentation.lib" ]
  visibility = [ ":seglib" ]
}

group("seglib") {
  public_configs = [":seglib_import"]
}

copy("seglib_shared_lib_copy") {
  sources = [
    "//third_party/seglib/bin/tpxai_segmentation.dll",
    "//third_party/seglib/bin/DirectML.dll",
    "//third_party/seglib/bin/Microsoft.AI.MachineLearning.dll",
    "//third_party/seglib/bin/onnxruntime.dll",
    "//third_party/seglib/bin/openvino.dll",
    "//third_party/seglib/bin/openvino_intel_cpu_plugin.dll",
    "//third_party/seglib/bin/openvino_intel_gpu_plugin.dll",
    "//third_party/seglib/bin/openvino_onnx_frontend.dll",
    "//third_party/seglib/bin/tbb12.dll"
  ]
  outputs = [ "$root_out_dir/{{source_file_part}}" ]
}

copy("seglib_config_files_copy") {
  sources = [
    "//seglib/configs/win/seglib_win_segnetu.json",
    "//seglib/configs/win/seglib_win_segnetu.json.gpu",
    "//seglib/configs/win/seglib_win_segnetu.json.cpu",
    "//seglib/configs/win/seglib_win_segnetu.json.winml",
    "//seglib/configs/win/seglib_win_segnetu.json.segnet",
  ]
  outputs = [ "$root_out_dir/resources/{{source_file_part}}" ]
  public_deps = [ ":seglib" ]
}

copy("seglib_visiontalk_files_copy") {
  sources = [
    "//seglib/models/VisionTalk/vt_l.aes",
    "//seglib/models/VisionTalk/vt_m.aes",
    "//seglib/models/VisionTalk/vt_s.aes",
  ]
  outputs = [ "$root_out_dir/resources/VisionTalk/{{source_file_part}}" ]
  public_deps = [ ":seglib" ]
}

copy("seglib_segnet_files_copy") {
  sources = [
    "//seglib/models/SegNet/segnetu_256_21_10_2021.aes",
    "//seglib/models/SegNet/segnetu_dynamic_2022_09_01_fix.aes",
    "//seglib/models/SegNet/SegNetUSlimReversed_160_BCEWL_SP23AllData_ri_v114.aes",
  ]
  outputs = [ "$root_out_dir/resources/Segnet/{{source_file_part}}" ]
  public_deps = [ ":seglib" ]
}

bundle_data("seglib_resource_files_bundle") {
  sources = [
    "//seglib/configs/win/seglib_win_segnetu.json",
    "//seglib/models/VisionTalk/vt_l.aes",
  ]
  outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
  public_deps = [ ":seglib" ]
}

group("seglib_resource_files_copy") {
  public_deps = [
    ":seglib_config_files_copy",
    ":seglib_visiontalk_files_copy",
    ":seglib_segnet_files_copy",
  ]
}

bundle_data("seglib_lib_file_bundle") {
  sources = [
    "//third_party/seglib/bin/tpxai_segmentation.dll"
  ]
  outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
  public_deps = [ 
  ":seglib", ":seglib_shared_lib_copy",
  ":seglib_resource_files_copy"
  ]
}


source_set("win") {
  defines = [ "CAPTURE_IMPLEMENTATION" ]
  sources = [
    "capability_list_win.cc",
    "capability_list_win.h",
    "filter_base_win.cc",
    "filter_base_win.h",
    "gpu_memory_buffer_tracker_win.cc",
    "gpu_memory_buffer_tracker_win.h",
    "metrics.cc",
    "metrics.h",
    "pin_base_win.cc",
    "pin_base_win.h",
    "sink_filter_observer_win.h",
    "sink_filter_win.cc",
    "sink_filter_win.h",
    "sink_input_pin_win.cc",
    "sink_input_pin_win.h",
    "video_capture_device_factory_win.cc",
    "video_capture_device_factory_win.h",
    "video_capture_device_mf_win.cc",
    "video_capture_device_mf_win.h",
    "video_capture_device_utils_win.cc",
    "video_capture_device_utils_win.h",
    "video_capture_device_win.cc",
    "video_capture_device_win.h",
  ]

  public_deps = [
    "//media/capture:capture_base",
    "//media/capture:capture_device_specific",
    "//media/capture:capture_switches",
    ":seglib"
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//media",
    "//media/base/win:color_space_util_win",
    "//media/capture/mojom:image_capture",
    "//media/capture/mojom:image_capture_types",
    "//media/capture/mojom:video_capture",
    "//media/mojo/mojom",
    "//services/service_manager/public/cpp",
    "//services/video_capture/public/mojom",
    "//third_party/libyuv",
    "//ui/display",
    "//ui/gfx",
    ":seglib_resource_files_bundle",
    ":seglib_lib_file_bundle"
  ]

  libs = [
    "mf.lib",
    "mfplat.lib",
    "mfreadwrite.lib",
    "mfuuid.lib",
    "D3DCompiler.lib",
    "Windowscodecs.lib",
    "d3d11.lib",
    "dxgi.lib"
  ]

  ldflags = [
    "/DELAYLOAD:mf.dll",
    "/DELAYLOAD:mfplat.dll",
    "/DELAYLOAD:mfreadwrite.dll",
  ]
}

source_set("win_unittests") {
  defines = [ "CAPTURE_IMPLEMENTATION" ]
  sources = [
    "d3d_capture_test_utils.cc",
    "d3d_capture_test_utils.h",
    "gpu_memory_buffer_tracker_win_unittest.cc",
    "video_capture_device_factory_win_unittest.cc",
    "video_capture_device_mf_win_unittest.cc",
  ]

  deps = [
    "//base/test:test_support",
    "//gpu/command_buffer/client",
    "//media:test_support",
    "//media/capture",
    "//media/capture:test_support",
    "//media/capture/mojom:image_capture",
    "//media/capture/mojom:image_capture_types",
    "//mojo/core/embedder",
    "//services/video_capture/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/libyuv",
    "//ui/gfx:test_support",
  ]

  libs = [
    "mf.lib",
    "mfplat.lib",
    "mfreadwrite.lib",
    "mfuuid.lib",
    "dxguid.lib",
  ]
  ldflags = [
    "/DELAYLOAD:mf.dll",
    "/DELAYLOAD:mfplat.dll",
    "/DELAYLOAD:mfreadwrite.dll",
  ]

  testonly = true
}
